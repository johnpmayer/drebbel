use std::str::FromStr;
use ast::*;

grammar;

Num: i64 = <s:r"[0-9]+"> => i64::from_str(s).unwrap();

Literal: Literal = {
    <n:Num> => Literal::Number(n),
};

Identifier: String = {
    <s:r"[a-z][a-zA-Z0-9_]*"> => String::from(s)
};

VariableName: VariableName = {
    Identifier => VariableName(<>)
};

SubroutineName: SubroutineName = {
    Identifier => SubroutineName(<>)
};

// Skipping the unary plus operator... it feels pointless
UnaryOp: UnaryOperator = {
    "-" => UnaryOperator::Minus
};

Factor: Box<Expression> = {
    VariableName => Box::new(Expression::Var(<>)),
    Literal => Box::new(Expression::Lit(<>)),
    UnaryOp Factor => Box::new(Expression::ApplyUnOp(<>)),
    "(" <Expression> ")",
};

FactorOp: InfixBinaryOperator = {
    "*" => InfixBinaryOperator::Mul,
    "/" => InfixBinaryOperator::Div,
    "%" => InfixBinaryOperator::Mod,
};

// Left-associative; Factor is on left, Term is on right
Term: Box<Expression> = {
    Term FactorOp Factor => Box::new(Expression::ApplyInfixBinOp(<>)),
    Factor,
};

TermOp: InfixBinaryOperator = {
    "+" => InfixBinaryOperator::Add,
    "-" => InfixBinaryOperator::Sub,
};

// Left-associative; Expression is on left, Factor is on right
pub Expression: Box<Expression> = {
    Expression TermOp Term => Box::new(Expression::ApplyInfixBinOp(<>)),
    Term,
};

pub Statement: Statement = {
    <v:VariableName> ":=" <expr:Expression> => Statement::Assignment(v, expr),
    "RETURN" <expr:Expression?> => Statement::Return(expr),
    => Statement::Empty,
};

StatementList: StatementList = {
    Statement => StatementList::Single(<>),
    <s:Statement> ";" <sl:StatementList> => StatementList::Sequence(s, Box::new(sl)),
};

CompoundStatement: CompoundStatement = {
    "BEGIN" <sl:StatementList> "END" => CompoundStatement(sl),
};

Subroutine: (SubroutineName, Subroutine) = {
    "SUB" <sn:SubroutineName> <vars:VariableName*> <block:CompoundStatement> "." => (sn, Subroutine {
        parameters: vars,
        block: block
    })
};

pub Program: Program = {
    <subs:Subroutine*> <cs:CompoundStatement> "." => Program {
        subroutines: subs.iter().cloned().collect(),
        entry: cs
    }
};