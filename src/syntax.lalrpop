use std::str::FromStr;
use ast::*;

grammar;

// Utilities

Comma<T>: Vec<T> = { // (0)
    <v:(<T> ",")*> <e:T?> => match e { // (1)
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

// Literals and Identifiers

Num: i64 = <s:r"[0-9]+"> => i64::from_str(s).unwrap();

Literal: Literal = <n:Num> => Literal::Number(n);

Identifier: String = <s:r"[a-z][a-zA-Z0-9_]*"> => String::from(s);

VariableName: VariableName = Identifier => VariableName(<>);

SubroutineName: SubroutineName = Identifier => SubroutineName(<>);

Symbol: Symbol = Identifier => Symbol(<>);

// Expressions with precedence rules (higher precedence earlier in the program)

UnaryOp1: UnaryOperator = {
    "-" => UnaryOperator::Minus,
    "!" => UnaryOperator::Not,
    "*" => UnaryOperator::Deref,
    "newref" => UnaryOperator::NewRef,
};

ExpressionSimple: Box<Expression> = {
    VariableName => Box::new(Expression::Var(<>)),
    Literal => Box::new(Expression::Lit(<>)),
    "(" <Expression> ")",
};

CallSubroutine: Box<Expression> =  {
    <sub:SubroutineName> "(" <parameters:Comma<Expression>> ")" =>
        Box::new(Expression::CallSubByValue(sub, parameters)),
};

MakeContinuation: Box<Expression> = {
    "CONT" <scope:Symbol> <sub:SubroutineName> "(" <parameters:Comma<Expression>> ")" =>
        Box::new(Expression::MakeCont(scope, sub, parameters)),
};

Expression0: Box<Expression> = {
    CallSubroutine,
    MakeContinuation,
    <e_obj:ExpressionSimple> "[" <e_index:Expression> "]" => Box::new(Expression::Subscript(e_obj, e_index)),
    ExpressionSimple,
};

Expression1: Box<Expression> = {
    UnaryOp1 Expression1 => Box::new(Expression::ApplyUnOp(<>)),
    Expression0,
};

BinaryOp2: InfixBinaryOperator = {
    "*" => InfixBinaryOperator::Mul,
    "/" => InfixBinaryOperator::Div,
    "%" => InfixBinaryOperator::Mod,
    "&&" => InfixBinaryOperator::And,
};

Expression2: Box<Expression> = {
    Expression2 BinaryOp2 Expression1 => Box::new(Expression::ApplyInfixBinOp(<>)), // Left-assoc
    Expression1,
};

BinaryOp3: InfixBinaryOperator = {
    "+" => InfixBinaryOperator::Add,
    "-" => InfixBinaryOperator::Sub,
    "||" => InfixBinaryOperator::Or,
};

Expression3: Box<Expression> = {
    Expression3 BinaryOp3 Expression2 => Box::new(Expression::ApplyInfixBinOp(<>)), // Left-assoc
    Expression2,
};

BinaryOp4: InfixBinaryOperator = {
    "==" => InfixBinaryOperator::Eq,
    "<" => InfixBinaryOperator::Lt,
};

Expression4: Box<Expression> = {
    Expression4 BinaryOp4 Expression3 => Box::new(Expression::ApplyInfixBinOp(<>)), // Left-assoc
    Expression3,
};

Expression5: Box<Expression> = {
    "IFE" <expr_test:Expression> "THEN" <expr_true:Expression> "ELSE" <expr_false:Expression4> "EFI" =>
        Box::new(Expression::Conditional(expr_test, expr_true, expr_false)),
    "RUN" <cont:Expression4> => Box::new(Expression::RunCont(cont)),
    "ISDONE" <cont:Expression4> => Box::new(Expression::IsDoneCont(cont)),
    "LASTVALUE" <cont:Expression4> => Box::new(Expression::LastValueCont(cont)),
    Expression4,
};

// Left-associative; Expression is on left, Factor is on right
pub Expression: Box<Expression> = Expression5;

pub Statement: Statement = {
    <ae:Expression> ":=" <expr:Expression> => Statement::Assignment(ae, expr),
    Expression => Statement::EvaluateIgnore(<>),
    "RETURN" <expr:Expression?> => Statement::Return(expr),
    "SUSPEND" <scope:Symbol> <expr:Expression?> => Statement::SuspendCont(scope, expr),
    Loop,
    Conditional,
    => Statement::Empty,
};

StatementList: StatementList = {
    Statement => StatementList::Single(<>),
    <s:Statement> ";" <sl:StatementList> => StatementList::Sequence(s, Box::new(sl)),
};

CompoundStatement: CompoundStatement = {
    "BEGIN" <sl:StatementList> "END" => CompoundStatement {
        statement_list: sl
    },
};

Loop: Statement = {
    "WHILE" <test_expr:Expression> "DO" <sl:StatementList> "DONE" =>
        Statement::Loop(test_expr, Box::new(sl)),
};

Conditional: Statement = {
    "IF" <expr_test:Expression> "THEN" <stmts_true:StatementList> "ELSE" <stmts_false:StatementList> "FI" =>
        Statement::Conditional(expr_test, Box::new(stmts_true), Box::new(stmts_false)),
};

Subroutine: (SubroutineName, Subroutine) = {
    "SUB" <sn:SubroutineName> "(" <arguments:Comma<VariableName>> ")" <block:CompoundStatement> "." => (sn, Subroutine {
        arguments: arguments,
        implementation: Implementation::Block(block)
    })
};

pub Program: Program = {
    <subs:Subroutine*> <cs:CompoundStatement> "." => Program {
        subroutines: subs.iter().cloned().collect(), // convert to a Vec<(k,v)> to HashMap<k,v>
        entry: cs
    }
};